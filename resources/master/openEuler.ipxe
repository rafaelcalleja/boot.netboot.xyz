#!ipxe

# openEuler
# https://openeuler.org/

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}:::none nameserver=${dns}
set ipparam BOOTIF=${netX/mac} ${ipparam}

goto ${menu} ||

:openEuler
clear osversion
set os openEuler
menu ${os} - ${arch}
item openEuler-23.09 ${space} ${os} openEuler-23.09
item openEuler-20.03-LTS-SP3 ${space} ${os} openEuler-20.03-LTS-SP3
item openEuler-22.03-LTS ${space} ${os} openEuler-22.03-LTS
item openEuler-22.03-LTS-SP1 ${space} ${os} openEuler-22.03-LTS-SP1
item openEuler-22.03-LTS-SP2 ${space} ${os} openEuler-22.03-LTS-SP2
isset ${osversion} || choose osversion || goto linux_menu
echo ${cls}
set dir ${osversion}/OS/${arch}
set repo ${openEuler_mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${arch} boot type
item graphical ${ova} graphical installer
item text ${ova} text based installer
item rescue ${ova} rescue
item kickstart ${ova} set kickstart url [ ${ks_url} ]
isset ${bt} || choose bt || goto openEuler
echo ${cls}
iseq ${bt} text && goto text ||
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
iseq ${bt} kickstart_device && goto kickstart_device ||
goto bootos_images

:graphical
set install_mode inst.graphical ||
goto bootos_images

:text
set install_mode inst.text ||
goto bootos_images

:rescue
set params inst.rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ks_url
set params inst.ks=${ks_url} ||
clear bt
goto boottype

:bootos_images
imgfree
kernel ${openEuler_mirror}/${dir}/images/pxeboot/vmlinuz inst.repo=${repo} ${install_mode} ${params} ${ipparam} initrd=initrd.magic ${cmdline}
initrd ${openEuler_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto linux_menu

:linux_menu
clear menu
exit 0
