#!ipxe

# AlmaLinux Operating System
# https://almalinux.org/

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}:::none nameserver=${dns}
set ipparam BOOTIF=${netX/mac} ${ipparam}

goto ${menu} ||

:almalinux
clear osversion
set os AlmaLinux
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch x86_64 ||
iseq ${os_arch} arm64 && set os_arch aarch64 ||
menu ${os} - ${os_arch}
item 9 ${space} ${os} 9 (Latest)
item 8 ${space} ${os} 8 (Latest)
isset ${osversion} || choose osversion || goto linux_menu
echo ${cls}
set dir ${almalinux_base_dir}/${osversion}/BaseOS/${os_arch}/os
set repo ${almalinux_mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${os_arch} boot type
item graphical ${ova} graphical installer
item text ${ova} text based installer
item rescue ${ova} rescue
item kickstart ${ova} set kickstart url [ ${ks_url} ]
isset ${bt} || choose bt || goto almalinux
echo ${cls}
iseq ${bt} text && goto text ||
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
iseq ${bt} kickstart_device && goto kickstart_device ||
goto bootos_images

:graphical
set install_mode inst.graphical ||
goto bootos_images

:text
set install_mode inst.text ||
goto bootos_images

:rescue
set params inst.rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ks_url
set params inst.ks=${ks_url} ||
clear bt
goto boottype

:bootos_images
imgfree
kernel ${almalinux_mirror}/${dir}/images/pxeboot/vmlinuz inst.repo=${repo} ${install_mode} ${params} ${ipparam} initrd=initrd.magic ${cmdline}
initrd ${almalinux_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto linux_menu

:linux_menu
clear menu
exit 0
